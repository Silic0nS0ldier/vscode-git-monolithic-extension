load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config", "ts_project")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//packages/git:ava/package_json.bzl", "bin")
load("//build_defs/lint:linters.bzl", "eslint_test")
load("@aspect_rules_js//js:defs.bzl", "js_test")

npm_link_all_packages(name = "node_modules")

npm_package(
    name = "pkg",
    srcs = [
        "package.json",
        ":lib",
    ],
    exclude_srcs_patterns = ["tsconfig.json"],
    include_types = True,
    visibility = ["//visibility:public"],
)

ts_project(
    name = "lib",
    srcs = glob(
        ["src/**/*.ts"],
        exclude = [
            "src/**/*.test.ts",
            "src/**/*.stub.ts",
        ],
    ) + ["package.json"],
    declaration = True,
    out_dir = "dist",
    root_dir = "src",
    tsconfig = ":tsconfig",
    deps = [
        ":node_modules/@formatjs/intl-durationformat",
        ":node_modules/@js-temporal/polyfill",
        ":node_modules/@types/node",
        ":node_modules/@types/which",
        ":node_modules/compare-versions",
        ":node_modules/get-stream",
        ":node_modules/which",
    ],
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = [":__pkg__"],
    deps = ["//:tsconfig"],
)

ts_project(
    name = "lib_tests",
    srcs = glob(
        [
            "src/**/*.test.ts",
            "src/**/*.stub.ts",
        ],
        exclude = ["src/**/*.it.*.ts"],
    ) + ["package.json"],
    declaration = True,
    out_dir = "dist",
    root_dir = "src",
    tsconfig = ":tsconfig",
    deps = [
        ":lib",
        ":node_modules/ava",
        ":node_modules/into-stream",
    ],
)

bin.ava_test(
    name = "tests",
    chdir = package_name(),
    data = [
        "package.json",
        ":lib_tests",
    ],
)

ts_project(
    name = "lib_integration_tests",
    srcs = glob(["src/**/*.it.*.ts"]) + ["package.json"],
    declaration = True,
    out_dir = "dist",
    root_dir = "src",
    tsconfig = ":tsconfig",
    deps = [
        ":lib",
        ":node_modules/@bazel/runfiles",
    ],
)

test_suite(
    name = "integration_tests",
    tests = [":integration_tests_" + str(i) for i, _ in enumerate(glob(["src/**/*.it.test.ts"]))],
)

[
    js_test(
        name = "integration_tests_" + str(i),
        data = [
            "package.json",
            ":lib_integration_tests",
            "@git",
        ],
        entry_point = s.replace("src/", "dist/").replace(".ts", ".js"),
        args = [
            "--test",
        ]
    ) for i, s in enumerate(glob(["src/**/*.it.test.ts"]))
]

eslint_test(
    name = "eslint",
    srcs = [
        ":lib",
        ":lib_integration_tests",
        ":lib_tests",
    ],
)
