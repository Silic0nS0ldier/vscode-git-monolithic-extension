"""
Rollup bundle rule.
"""

load("@aspect_rules_js//js:defs.bzl", "js_run_binary")

def _vsix_package(name, srcs, in_dir, **kwargs):
    # type: (string, list[string], string) -> None
    args = [
        "--in-dir",
        in_dir,
        "--out-file",
        "%s.vsix" % name,
    ]
    outs = ["%s.vsix" % name]

    # Remove `None` values generated by `macro`
    clean_kwargs = {}
    for key, value in kwargs.items():
        if value != None:
            clean_kwargs[key] = value

    js_run_binary(
        name = name,
        tool = "//build_defs/vsix_package:bin",
        args = args,
        srcs = srcs,
        outs = outs,
        chdir = native.package_name(),
        mnemonic = "VsixPackage",
        stamp = -1,
        # Removes an intermediary `_js_library` target that may fail to find toolchains
        use_execroot_entry_point = False,
        **clean_kwargs
    )

vsix_package = macro(
    implementation = _vsix_package,
    inherit_attrs = "common",
    attrs = {
        "srcs": attr.label_list(
            mandatory = True,
            allow_files = True,
            doc = "A list of source files to bundle.",
        ),
        "in_dir": attr.string(
            mandatory = True,
            configurable = False,
            doc = "The directory containing the extension to package.",
        ),
    },
    doc = """
        Create a Visual studio Code extension package.
    """,
)
