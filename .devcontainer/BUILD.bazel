load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_load", "oci_push")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@aspect_rules_js//js:defs.bzl", "js_test")

pkg_tar(
    name = "layer_bazelisk",
    srcs = [
        "@multitool//tools/bazelisk",
    ],
    extension = "tar.gz",
    package_dir = "/usr/local/bin",
    symlinks = {
        "bazel": "bazelisk",
    },
)

pkg_tar(
    name = "layer_buildtools",
    srcs = [
        "@multitool//tools/buildifier",
        "@multitool//tools/buildozer",
        "@multitool//tools/unused_deps",
    ],
    extension = "tar.gz",
    package_dir = "/usr/local/bin",
)

pkg_tar(
    name = "layer_starpls",
    srcs = [
        "@multitool//tools/starpls",
    ],
    extension = "tar.gz",
    package_dir = "/usr/local/bin",
)

pkg_tar(
    name = "layer_nodejs",
    srcs = select(
        {
            "//build_defs/config_settings:linux_amd64": ["@nodejs_linux_amd64//:node_bin"],
            "//build_defs/config_settings:linux_arm64": ["@nodejs_linux_arm64//:node_bin"],
        },
        no_match_error = "layer_nodejs only supports linux_amd64 and linux_arm64 platforms",
    ),
    extension = "tar.gz",
    package_dir = "/usr/local/bin",
)

pkg_tar(
    name = "layer_pnpm",
    srcs = [
        "@multitool//tools/pnpm",
    ],
    extension = "tar.gz",
    package_dir = "/usr/local/bin",
)

pkg_tar(
    name = "layer_multitool",
    srcs = [
        "@multitool//tools/multitool",
    ],
    extension = "tar.gz",
    package_dir = "/usr/local/bin",
)

oci_image(
    name = "single_platform",
    base = "@devcontainer_base_ubuntu",
    labels = {
        "devcontainer.metadata": json.encode([{
            "customizations": {
                "vscode": {
                    "extensions": [
                        "bazelbuild.vscode-bazel",
                        "ms-azuretools.vscode-docker",
                        "github.vscode-github-actions",
                    ],
                    "settings": {
                        "bazel.enableCodeLens": True,
                        "bazel.lsp.command": "starpls",
                        "bazel.lsp.args": ["server"],
                    },
                },
            },
            "mounts": [
                {
                    "source": "dind-var-lib-docker-${devcontainerId}",
                    "target": "/var/lib/docker",
                    "type": "volume",
                },
            ],
            "privileged": True,
            "containerEnv": {
                "DOCKER_BUILDKIT": "1",
            },
        }]),
    },
    tars = [
        ":layer_bazelisk",
        ":layer_buildtools",
        ":layer_starpls",
        ":layer_nodejs",
        ":layer_pnpm",
        ":layer_multitool",
        #"@layers_apt_ubuntu_jammy",
    ],
)

oci_image_index(
    name = "multi_platform",
    images = [":single_platform"],
    platforms = [
        "//build_defs/platforms:linux_amd64",
        "//build_defs/platforms:linux_arm64",
    ],
)

oci_load(
    name = "load",
    image = ":single_platform",
    repo_tags = ["silic0ns0ldier/vscode-git-monolithic-extension/devcontainer:dev"],
)

oci_push(
    name = "push",
    image = ":multi_platform",
    repository = "ghcr.io/silic0ns0ldier/vscode-git-monolithic-extension/devcontainer",
)

js_test(
    name = "check",
    entry_point = "devcontainer-check.mjs",
    chdir = package_name(),
    data = [
        "devcontainer.json",
        ":multi_platform.digest",
    ]
)
